
allprojects {
  task tidy() {
    description "Applies formatters and cleanups to sources (if configured)."
    group "verification"
  }

  tasks.matching { task -> task.name == "spotlessApply" }.configureEach { v ->
    tidy.dependsOn v
  }

  tasks.matching { task -> task.name == "spotlessCheck" }.configureEach { v ->
    check.dependsOn v
  }
}

allprojects { prj ->
  plugins.withType(JavaPlugin) {
    prj.apply plugin: 'com.diffplug.spotless'

    spotless {
      java {
        googleJavaFormat(deps.versions.googleJavaFormat.get())

        licenseHeaderFile licenseHeader
        lineEndings 'UNIX'
        endWithNewline()
      }
    }
  }
}

configure(project(":gradle")) { prj ->
  prj.apply plugin: 'groovy'
  prj.apply plugin: 'com.diffplug.spotless'

  spotless {
    groovy {
      excludeJava()
      greclipse(deps.versions.greclipse.get())

      licenseHeaderFile licenseHeader
      lineEndings 'UNIX'
      endWithNewline()
    }

    groovyGradle {
      target '*.gradle', 'gradle/**/*.gradle'

      greclipse(deps.versions.greclipse.get()).configFile rootProject.files(
          'gradle/validation/spotless/spotless.eclipseformat.xml',
          'gradle/validation/spotless/spotless.groovyformat.prefs')
    }
  }
}